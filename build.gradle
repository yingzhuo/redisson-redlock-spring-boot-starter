//file:noinspection GroovyAssignabilityCheck

plugins {
	id 'java'
	id 'signing'
	id 'maven-publish'
	id 'io.spring.dependency-management'
}

group = 'com.github.yingzhuo'
version = '3.45.0'

ext {
	set('pomGroupId', "com.github.yingzhuo")
	set('pomVersion', "${project.version}")
	set('pomArtifactId', 'redisson-redlock-spring-boot-starter')
	set('pomArtifactName', 'redisson-redlock-spring-boot-starter')
}

dependencyManagement {
	imports {
		mavenBom 'org.springframework.boot:spring-boot-dependencies:3.4.3'
	}
}

dependencies {
	// spring-boot & spring
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
	compileOnly 'org.springframework.boot:spring-boot-starter-aop'
	compileOnly 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.springframework.boot:spring-boot-starter-security'
	compileOnly 'org.springframework.boot:spring-boot-starter-logging'
	compileOnly 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.springframework.boot:spring-boot-starter-jdbc'
	compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
	compileOnly 'org.springframework.boot:spring-boot-starter-data-redis'
	compileOnly 'org.springframework.boot:spring-boot-autoconfigure-processor'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	// redisson
	implementation 'org.redisson:redisson:3.45.0'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// slf4j
	implementation 'org.slf4j:slf4j-api'
}

java {
	withJavadocJar()
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	toolchain { spec ->
		languageVersion.set(JavaLanguageVersion.of(17))
	}
}

sourcesJar {
}

javadoc {
	if (JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
	options.addBooleanOption('Xdoclint:none', true)
}

test {
	useJUnitPlatform()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "${pomGroupId}"
			artifactId = "${pomArtifactId}"
			version = "${pomVersion}"

			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}

			from components.java

			pom {
				name = "${pomArtifactName}"
				description = 'spring-boot starter for redisson RedLock/MultiLock'
				url = 'https://github.com/yingzhuo/redisson-redlock-spring-boot-starter'
				inceptionYear = '2025'

				licenses {
					license {
						name = 'Apache License, Version 2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'yingzhuo'
						name = '应卓'
						email = 'yingzhor@gmail.com'
					}
				}
				scm {
					url = 'git@github.com:yingzhuo/redisson-redlock-spring-boot-starter.git'
					connection = 'scm:git:git@github.com:yingzhuo/redisson-redlock-spring-boot-starter.git'
					developerConnection = 'scm:git:git@github.com:yingzhuo/redisson-redlock-spring-boot-starter.git'
				}
			}
		}
	}

	repositories {
		maven {
			name = 'OSSRH'
			url = rootProject.version.endsWith('SNAPSHOT') ? "${ossrhSnapshotUrl}" : "${ossrhReleaseUrl}"

			credentials {
				// 私密信息在 $GRADLE_USER_HOME/gradle.properties
				username = "${ossrhTokenUsername}"
				password = "${ossrhTokenPassword}"
			}
		}
	}
}

signing {
	useGpgCmd()
	sign publishing.publications.mavenJava
}

tasks.withType(GenerateModuleMetadata).configureEach {
	enabled = false
}
